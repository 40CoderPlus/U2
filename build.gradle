buildscript {
    repositories {
        gradlePluginPortal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.1.0"
        classpath "com.palantir.baseline:gradle-baseline-java:4.185.0"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:6.12.1"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.5.0.2730"
        classpath 'org.owasp:dependency-check-gradle:7.4.4'
    }
}

subprojects {
    group 'com.fortycoderplus.u2'
    version "1.0.0"

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'idea'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencyManagement {
    }

    dependencies {
        // lombok
        compileOnly "org.projectlombok:lombok:1.18.24"
        annotationProcessor "org.projectlombok:lombok:1.18.24"
        // mapstruct
        implementation 'org.mapstruct:mapstruct:1.5.3.Final'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

        // test:junit
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
    }

    plugins.withType(JavaPlugin) {
        project.sourceCompatibility = JavaVersion.VERSION_11
    }
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    java {
        withSourcesJar()
    }
    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = 'com.fortycoderplus.u2'
                artifactId = project.name
                version = project.version

                from components.java

                pom {
                    name = "Unified UDF :: $project.name"
                    description = "Unified UDF :: $project.name"
                    url = 'https://github.com/40CoderPlus/u2'
                    developers {
                        developer {
                            id = 'Codingendless'
                            name = 'Codingendless'
                            email = 'codingendless@40coderplus.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:40CoderPlus/u2.git'
                        url = 'https://github.com/40CoderPlus/u2'
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username "$nexusUsername"
                    password "$nexusPassword"
                }
                def releasesRepoUrl = "https://maven.40coderplus.com/repository/maven-releases/"
                def snapshotsRepoUrl = "https://maven.40coderplus.com/repository/maven-snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                allowInsecureProtocol = true
            }
        }
    }
}

apply from: 'baseline.gradle'
apply from: 'sonar.gradle'